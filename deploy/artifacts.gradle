/*
 * *************************************************************************************************
 *                                 Copyright 2018 Universum Studios
 * *************************************************************************************************
 *                  Licensed under the Apache License, Version 2.0 (the "License")
 * -------------------------------------------------------------------------------------------------
 * You may not use this file except in compliance with the License. You may obtain a copy of the
 * License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License
 * is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
 * or implied.
 *
 * See the License for the specific language governing permissions and limitations under the License.
 * *************************************************************************************************
 */

final boolean isModule = project.name.startsWith('library-') && !project.name.endsWith('_group')
final boolean isPrimaryModule = project.name == 'library'

/**
 * Prepares (copies and renames classes.jar) file for classesJar task for extraction.
 */
task prepareClassesZip(type: Copy, dependsOn: ":${project.name}:bundleReleaseAar") {
    from "${buildDir}/intermediates/aar_main_jar/release/classes.jar"
    into "${buildDir}/tmp/release"
    rename { fileName -> fileName.replace('.jar', '.zip')}
}

/**
 * Bundles modules's classes into single JAR file.
 */
task classesJar(type: Jar, dependsOn: prepareClassesZip) {
    group 'artifact'
    description "Bundles classes into 'library.jar' like file."
    from zipTree("${buildDir}/tmp/release/classes.zip")
}

/**
 * Generates javadoc documentation for the current project.
 */
task generateJavadoc(type: Javadoc) {
    source isModule ? android.sourceSets.main.java.srcDirs : android.sourceSets.release.java.srcDirs
    options {
        title = createJavadocTitle()
        header = config.javadoc.header
        author = true
        memberLevel = JavadocMemberLevel.PROTECTED
        if (!isPrimaryModule) {
            links config.javadoc.reference.online
        }
        links 'https://docs.oracle.com/javase/8/docs/api'
        links 'https://developer.android.com/reference/packages'
        links 'https://developer.android.com/reference/androidx/packages'
        links 'https://universum-studios.bitbucket.io/android/library/logger/reference/1.x'
    }
    // Append class path with dependencies.
    classpath += files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.buildType.name == 'release') classpath += variant.getCompileClasspath(null)
    }
    failOnError false
}

/**
 * Function which creates title for Javadoc document specific for the current project module.
 *
 * @return Project specific Javadoc title.
 */
def createJavadocTitle() {
    def moduleName = project.name
    if (moduleName.startsWith("library")) {
        if (moduleName == 'library') return config.javadoc.title
        else return "${config.pom.project.name} (${moduleName.replace("library-", "").replace("_group", "")}) API"
    }
    return "Documentation"
}

/**
 * Bundles modules's Javadoc into single JAR file.
 */
task javadocJar(type: Jar) {
    group 'artifact'
    description "Bundles Javadoc into 'library-javadoc.jar' like file."
    archiveClassifier = 'javadoc'
    from generateJavadoc
}

/**
 * Bundles modules's Raw sources into JAR file.
 */
task sourcesJar(type: Jar) {
    group 'artifact'
    description "Bundles Raw sources into 'library-sources.jar' like file."
    archiveClassifier = 'sources'
    from isModule ? android.sourceSets.main.java.srcDirs : android.sourceSets.release.java.srcDirs
    from isModule ? android.sourceSets.main.res.srcDirs : android.sourceSets.release.res.srcDirs
}

/**
 * Bundles modules's release artifacts (classes.jar, sources.jar, javadoc.jar) into single ZIP file.
 */
task artifactsZip(type: Zip) {
    group 'artifact'
    description "Bundles release artifacts into 'library-major.minor.patch.zip' like file."
    from fileTree(
            dir: "${repos.project.artifacts}/universum/studios/android/${config.library.name}/${config.pom.version}",
            excludes: ['*.pom', '*.md5', '*.sha1']
    )
    into "${config.library.name}-${config.pom.version}"
    archiveFileName = "${config.library.name}-${config.pom.version}.zip"
    destinationDirectory = file("${buildDir}/tmp/release/artifacts")
}

/**
 * Set of artifacts to be generated.
 */
artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}